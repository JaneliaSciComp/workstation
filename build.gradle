buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'com.offbynull.coroutines',  name: 'gradle-plugin',  version: '1.2.2'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    group = 'org.janelia.jacs2'
    version = '2.0.0'
}

project.ext {
        eclipseBuildDir = 'eclipseBuild'
}


subprojects {
    apply plugin: 'eclipse'
    apply from: "${rootDir}/deps.gradle"
    apply plugin: "coroutines"

    coroutines {
        debugMode = false
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
                srcDir file('src/integration-test/java')
            }
            resources {
                srcDir file('src/integration-test/resources')
            }
        }
    }

    configurations {
        provided
        compile.extendsFrom provided
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    eclipse {
        classpath {
            defaultOutputDir = file(eclipseBuildDir)
            downloadSources = true
            downloadJavadoc = true
        }
        project {
            name = "$project.name"
        }
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task integrationTest(type: Test) {
        testClassesDir = sourceSets.integrationTest.output.classesDir
        classpath = sourceSets.integrationTest.runtimeClasspath
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.1"
}
